// Characters
norwegian_char = _{ "æ" | "ø" | "å" | "Æ" | "Ø" | "Å" }
p_char         = _{ (!(NEWLINE | " ") ~ ANY)+ }
c_char         = _{ (!(NEWLINE | " " | "`") ~ ANY)+ }
i_char         = _{ (!(NEWLINE | " " | "_" | "*") ~ ANY)+ }
b_char         = _{ (!(NEWLINE | " " | "**") ~ ANY)+ }
s_char         = _{ (!(NEWLINE | " " | "~~") ~ ANY)+ }
c_line_char    = _{ (!(NEWLINE | "```") ~ ANY)+ }
comment_char   = _{ (!(NEWLINE | "-->") ~ ANY)+ }
table_char     = _{ (!(NEWLINE | "|") ~ ANY)+ }

link_char = _{ ASCII_ALPHANUMERIC | norwegian_char | "#" | "_" | "|" | "{" | "}" | "/" | ":" | ";" | "-" | "." | "," | "-" }
digit     = _{ '0'..'9' }
indent    =  { " "* }

// Words
word               = {
    !(forbidden_sentence_prefix | bold | italic | strikethrough | code | link) ~ NEWLINE? ~ " "* ~ p_char+ ~ " "?
}
italic_word        = { i_char+ ~ " "? }
bold_word          = { b_char+ ~ " "? }
strikethrough_word = { s_char+ ~ " "? }
code_word          = { c_char+ ~ " "? }
link_word          = { (link_char | " " | "(" | ")")+ }
link_data          = { (link_char | "@")+ }

// Prefixes
task_open         =  { "- [ ] " }
task_complete     =  { "- [x] " | "- [X] " }
task_prefix       = _{ task_open | task_complete }
quote_prefix      =  { ">" }
code_block_prefix =  { "```" }
table_prefix      =  { "|" }
list_prefix       =  { (" "* ~ "-") | (NUMBER ~ ". ") }
heading_prefix    =  { "#" }

forbidden_sentence_prefix = {
    NEWLINE ~ (task_prefix | quote_prefix | code_block_prefix | table_prefix | list_prefix | heading_prefix)
}

// Lines
code                 =  { NEWLINE? ~ "`" ~ code_word+ ~ "`" }
programming_language =  { ASCII_ALPHA+ }
code_line            =  { (c_line_char | " ")+ }
normal               = _{ word+ }
markdown_link        =  { "[" ~ link_word+ ~ "]" ~ "(#" ~ link_data+ ~ ")" }
external_link        =  { "[" ~ link_word+ ~ "]" ~ "(" ~ link_data+ ~ ")" }
link                 = _{ NEWLINE? ~ (markdown_link | external_link) }
table_word           =  { (table_char | code | link | " ")+ }
italic               =  { NEWLINE? ~ (("*" ~ (italic_word | NEWLINE)+ ~ "*") | ("_" ~ (italic_word | NEWLINE)+ ~ "_")) }
bold                 =  { NEWLINE? ~ "**" ~ (bold_word | NEWLINE)+ ~ "**" }
strikethrough        =  { NEWLINE? ~ "~~" ~ (strikethrough_word | NEWLINE)+ ~ "~~" }
o_list_counter       =  { digit+ ~ ". " }

sentence = _{ (code | link | italic | bold | strikethrough | normal)+ }

table_row       = { "|" ~ (table_word | " " | "|")+ }
table_seperator = { ("|"? ~ " "? ~ "-"+ ~ " "? ~ "|")+ }

u_list = { indent ~ ("-" | "*") ~ " " ~ sentence+ }
o_list = { indent ~ o_list_counter ~ sentence+ }

h1 = _{ "# " ~ (sentence | " ")+ }
h2 = _{ "## " ~ (sentence | " ")+ }
h3 = _{ "### " ~ (sentence | " ")+ }
h4 = _{ "#### " ~ (sentence | " ")+ }
h5 = _{ "##### " ~ (sentence | " ")+ }
h6 = _{ "###### " ~ (sentence | " ")+ }

// Blocks
heading        = { (h1 | h2 | h3 | h4 | h5 | h6) }
list_container = { (NEWLINE? ~ (u_list | o_list))+ }
paragraph      = { sentence+ }
code_block     = { "```" ~ (programming_language | NEWLINE) ~ (code_line | NEWLINE)+ ~ "```" }
table          = { (NEWLINE? ~ (table_seperator | table_row))+ }
quote          = { NEWLINE? ~ ">" ~ (sentence | " ")+ }
task           = { NEWLINE? ~ task_prefix ~ sentence }
block_sep      = { NEWLINE }
horizontal_sep = { "---"+ }

comment = _{ "<!--" ~ (comment_char)+ ~ "-->" }

txt = {
    (horizontal_sep | task | comment | table | quote | list_container | code_block | heading | paragraph | block_sep+)+
}
